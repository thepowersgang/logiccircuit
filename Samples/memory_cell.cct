; Latch
; Usage: LATCH <clear>, <set>
; - pulse:	Signal to propogate for delays
; - clear:	Set to 1 to reset the latch to 0
; - set:	Set to 1 to set the latch to 1
; Delays:
; - Writing: 3
; - Reading: 0
#defunit LATCH
#input $pulse, $clr, $set
#output $ready, $ret

; Ensure that lines are held for 2 cycles
$_set = DELAY $set
$_clr = DELAY $clr

; Have two latches (1 cycle out of sync) to hold better
$value = AND (NOT $clr), (OR $value, $set)
$value2 = AND (NOT $_clr), (OR $value, $_set)

$ret = DELAY $value
$ret = DELAY $value2
$ready = DELAY{3} $pulse

#endunit

; Memory Cell
; Usage: MEMORY_CELL <enable>, <write>, <value>
; - enable:	Set to 1 to read/write from the cell
; - write:	Set to 1 to set the cell
; - value:	New cell contents (ignored if <write> is not set)
; Returns:
; - $ready:	Delayed $enable signal, raised when data is ready
; - $ret:	Cell value
; Delays:
; - Writing: 6
; - Reading: 6

#defunit MEMORY_CELL
#input $enable, $write, $datain	; set input lines
#output $ready, $ret	; Set output lines

$reset = AND (DELAY $enable), (DELAY $write), (NOT $datain)
$set = AND (DELAY $enable), (DELAY $write), (DELAY $datain)

$_ready, $value = LATCH $enable, $reset, $set

; State: 0
; W V | Out
; 0 0 | 0
; 0 1 | 0
; 1 0 | 0
; 1 1 | 1
; State: 1
; W V | Out
; 0 0 | 1
; 0 1 | 1
; 1 0 | 0
; 1 1 | 1

$ret = AND $_ready, $value
$ready = DELAY $_ready

#endunit
